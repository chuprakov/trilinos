INCLUDE(TrilinosPackageHelpers)
INCLUDE(Trilinos_Add_Option)

#
# A) Define the package
#

TRILINOS_PACKAGE(Teuchos CLEANED)

SET(${PACKAGE_NAME}_VERSION "1.5d")

#
# B) Platform-specific checks
#

INCLUDE(CheckForAbiDemangle)
CHECK_FOR_ABI_DEMANGLE(HAVE_GCC_ABI_DEMANGLE)

INCLUDE(CheckBlasFloat)
CHECK_BLAS_FLOAT(HAVE_TEUCHOS_BLASFLOAT)

INCLUDE(CheckCXXComplexBlasProblem)
CHECK_CXX_COMPLEX_BLAS_PROBLEM(HAVE_COMPLEX_BLAS_PROBLEM)

IF (HAVE_COMPLEX_BLAS_PROBLEM)
  INCLUDE(CheckCXXComplexBlasProblemCanBeFixed)
  CHECK_CXX_COMPLEX_BLAS_PROBLEM_CAN_BE_FIXED(
    HAVE_FIXABLE_COMPLEX_BLAS_PROBLEM)
ENDIF()


#
# C) Set up package-specific options
#

TRILINOS_ADD_OPTION(
  ${PACKAGE_NAME}_ENABLE_DEBUG
  HAVE_TEUCHOS_DEBUG
  "Enable a host of runtime debug checking."
  ${Trilinos_ENABLE_DEBUG}
  )

TRILINOS_ADD_OPTION(
  ${PACKAGE_NAME}_ENABLE_EXPLICIT_INSTANTIATION
  HAVE_TEUCHOS_EXPLICIT_INSTANTIATION
  "Turn on explicit instantiation in Teuchos."
  ${Trilinos_ENABLE_EXPLICIT_INSTANTIATION}
  )

OPTION(
  ${PACKAGE_NAME}_ENABLE_PERFORMANCE_TESTS
  "Override enable ${PACKAGE_NAME} performance tests."
  ${Trilinos_ENABLE_PERFORMANCE_TESTS}
  )

TRILINOS_ADD_OPTION(
  ${PACKAGE_NAME}_ENABLE_COMPLEX
  HAVE_TEUCHOS_COMPLEX
  "Enable Teuchos features for complex arithmetic."
  ON
  )

TRILINOS_ADD_OPTION(
  ${PACKAGE_NAME}_ENABLE_EXTENDED
  HAVE_TEUCHOS_EXTENDED
  "Enable extended Teuchos features."
  ON
  )

TRILINOS_ADD_OPTION(
  ${PACKAGE_NAME}_ENABLE_GCC_DEMANGLE
  HAVE_TEUCHOS_DEMANGLE
  "Enable Teuchos calls to GCC demangle library function if it exists (see HAVE_TEUCHOS_BLASFLOAT)."
  ON
  )

#
# D) Add the libraries, tests, and examples
#

ADD_SUBDIRECTORY(src)

TRILINOS_PACKAGE_ADD_TEST_DIRECTORIES(test)

TRILINOS_PACKAGE_ADD_EXAMPLE_DIRECTORIES(example)

TRILINOS_PACKAGE_ADD_PERFORMANCE_TEST_DIRECTORIES(performance)

#
# E) Do standard postprocessing
#

TRILINOS_PACKAGE_POSTPROCESS()
