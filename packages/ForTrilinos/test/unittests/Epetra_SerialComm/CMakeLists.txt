## Change the TESTING_MODULE_NAME below and that's it.

INCLUDE(PackageAddExecutable)

INCLUDE_DIRECTORIES(
  ${CMAKE_CURRENT_SOURCE_DIR}
  ${CMAKE_CURRENT_SOURCE_DIR}/../UNITTEST_COMMON
  ${CMAKE_CURRENT_BINARY_DIR}
  )

## EDIT STATEMENT BELOW ONLY
SET(TESTING_MODULE_NAME Epetra_SerialComm)

######################################################################

SET(PREPROCESSING_INPUT ${CMAKE_CURRENT_SOURCE_DIR}/${TESTING_MODULE_NAME}_tests.un)
SET(FORTRAN_DRIVER_TEMPLATE ${CMAKE_CURRENT_SOURCE_DIR}/../UNITTEST_COMMON/unittest.F90)
SET(FORTRAN_DRIVER_STEM ${TESTING_MODULE_NAME}_tests)
SET(FORTRAN_CALLS_STEM ${TESTING_MODULE_NAME}_test_calls)
SET(FORTRAN_IMPLS_STEM ${TESTING_MODULE_NAME}_test_impls)
SET(CONFIG_FILE_PATH ${CMAKE_CURRENT_BINARY_DIR}/../../../src)
SET(RUNTIME_MACROS_PATH ${CMAKE_CURRENT_SOURCE_DIR}/../UNITTEST_COMMON)
SET(RUNTIME_MACROS_FILE ${RUNTIME_MACROS_PATH}/runtime_macros.h)

######################################################################

ADD_CUSTOM_COMMAND(
  OUTPUT ${FORTRAN_IMPLS_STEM}.F90 ${FORTRAN_CALLS_STEM}.F90 ${FORTRAN_DRIVER_STEM}.tests ${FORTRAN_DRIVER_STEM}.mpitests
  COMMAND ForTrilinos_unittest_parser ${TESTING_MODULE_NAME} ${CMAKE_CURRENT_SOURCE_DIR}
  DEPENDS ${PREPROCESSING_INPUT} ${RUNTIME_MACROS_FILE}
  )

ADD_CUSTOM_COMMAND(
  OUTPUT ${FORTRAN_IMPLS_STEM}.f90
  COMMAND cpp -I${RUNTIME_MACROS_PATH} -I${CONFIG_FILE_PATH} ${FORTRAN_IMPLS_STEM}.F90 ${FORTRILINOS_LINE_BREAKER} > ${FORTRAN_IMPLS_STEM}.f90
  DEPENDS ${PREPROCESSING_INPUT} ${RUNTIME_MACROS_FILE} ${FORTRAN_IMPLS_STEM}.F90
  )

SET_PROPERTY(
  SOURCE ${FORTRAN_DRIVER_TEMPLATE}
  APPEND PROPERTY COMPILE_DEFINITIONS TEST_CALLS_FILE=${TESTING_MODULE_NAME}_test_calls TEST_IMPLS_FILE_STR="${TESTING_MODULE_NAME}_test_impls"
  )

######################################################################

PACKAGE_ADD_EXECUTABLE(${FORTRAN_DRIVER_STEM}
  SOURCES
  ${FORTRAN_DRIVER_TEMPLATE}
  ${FORTRAN_CALLS_STEM}.F90
  ${FORTRAN_IMPLS_STEM}.f90
  ${FORTRAN_DRIVER_STEM}.tests
  ${FORTRAN_DRIVER_STEM}.mpitests
  ${RUNTIME_MACROS_FILE}
  ${PREPROCESSING_INPUT}
  NOEXEPREFIX
  )

TARGET_LINK_LIBRARIES(${FORTRAN_DRIVER_STEM} fortrilinos)

PACKAGE_ADD_TEST(
  ../UNITTEST_LAUNCHER/ForTrilinos_unittest_launcher_serial
  NOEXEPREFIX
  NAME ${FORTRAN_DRIVER_STEM}
  ARGS ${FORTRAN_DRIVER_STEM}.tests
  COMM serial
  PASS_REGULAR_EXPRESSION "END RESULT: ALL TESTS PASSED"
  )

PACKAGE_ADD_TEST(
  ${FORTRAN_DRIVER_STEM}
  NOEXEPREFIX
  NAME ${FORTRAN_DRIVER_STEM}
  ARGS "-f ${FORTRAN_DRIVER_STEM}.mpitests"
  COMM mpi
  NUM_MPI_PROCS 3
  PASS_REGULAR_EXPRESSION "END RESULT: ALL TESTS PASSED"
  )

######################################################################

