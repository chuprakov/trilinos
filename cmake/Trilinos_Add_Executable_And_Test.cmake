
INCLUDE(Trilinos_Add_Executable)
INCLUDE(Trilinos_Add_Test)


FUNCTION(TRILINOS_ADD_EXECUTABLE_AND_TEST EXE_NAME)
   
  #
  # A) Parse the input arguments
  #

  PARSE_ARGUMENTS(
     #prefix
     PARSE
     #lists
     "SOURCES;NUM_MPI_PROCS;DIRECTORY;KEYWORDS;COMM;ARGS;NAME;PASS_REGULAR_EXPRESSION;HOST;XHOST;FAIL_REGULAR_EXPRESSION"
     #options
     "STANDARD_PASS_OUTPUT"
     ${ARGN}
     )

  IF(Trilinos_VERBOSE_CONFIGURE)
    MESSAGE("")
    MESSAGE("TRILINOS_ADD_EXECUTABLE_AND_TEST: ${EXE_NAME}")
  ENDIF()

  #
  # B) Trilinos_Add_Executable(...)
  #

  SET(CALL_ARGS "")
  IF (PARSE_SOURCES)
    SET(CALL_ARGS ${CALL_ARGS} SOURCES ${PARSE_SOURCES}) 
  ENDIF()
  IF (PARSE_DIRECTORY)
    SET(CALL_ARGS ${CALL_ARGS} DIRECTORY ${PARSE_DIRECTORY}) 
  ENDIF()
  IF (PARSE_COMM)
    SET(CALL_ARGS ${CALL_ARGS} COMM ${PARSE_COMM}) 
  ENDIF()

  TRILINOS_ADD_EXECUTABLE(${EXE_NAME} ${CALL_ARGS})

  #
  # C) Trilinos_Add_Test(...)
  #

  SET(CALL_ARGS "")
  IF (PARSE_DIRECTORY)
    SET(CALL_ARGS ${CALL_ARGS} DIRECTORY ${PARSE_DIRECTORY}) 
  ENDIF()
  IF (PARSE_KEYWORDS)
    SET(CALL_ARGS ${CALL_ARGS} KEYWORDS ${PARSE_KEYWORDS}) 
  ENDIF()
  IF (PARSE_COMM)
    SET(CALL_ARGS ${CALL_ARGS} COMM ${PARSE_COMM}) 
  ENDIF()
  IF (PARSE_NUM_MPI_PROCS)
    SET(CALL_ARGS ${CALL_ARGS} NUM_MPI_PROCS ${PARSE_NUM_MPI_PROCS}) 
  ENDIF()
  IF (PARSE_ARGS)
    SET(CALL_ARGS ${CALL_ARGS} ARGS ${PARSE_ARGS}) 
  ENDIF()
  IF (PARSE_HOST)
    SET(CALL_ARGS ${CALL_ARGS} HOST ${PARSE_HOST}) 
  ENDIF()
  IF (PARSE_XHOST)
    SET(CALL_ARGS ${CALL_ARGS} XHOST ${PARSE_XHOST}) 
  ENDIF()
  IF (PARSE_PASS_REGULAR_EXPRESSION)
    SET(CALL_ARGS ${CALL_ARGS} PASS_REGULAR_EXPRESSION ${PARSE_PASS_REGULAR_EXPRESSION}) 
  ENDIF()
  IF (PARSE_FAIL_REGULAR_EXPRESSION)
    SET(CALL_ARGS ${CALL_ARGS} FAIL_REGULAR_EXPRESSION ${PARSE_FAIL_REGULAR_EXPRESSION}) 
  ENDIF()
  IF (PARSE_STANDARD_PASS_OUTPUT)
    SET(CALL_ARGS ${CALL_ARGS} STANDARD_PASS_OUTPUT)
  ENDIF()

  TRILINOS_ADD_TEST(${EXE_NAME} ${CALL_ARGS})

ENDFUNCTION()
